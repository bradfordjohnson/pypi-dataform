config {
    type: "view",
    dependencies: ["py"]
}

WITH
  ProjectCounts AS (
  SELECT
    CAST(timestamp AS DATE) AS date,
    country_code,
    project_name,
    COUNT(project_name) AS downloads
  FROM
    ${ref("py")}
  WHERE
    country_code IN ('US',
      'IN',
      'DE',
      'FR',
      'CA',
      'GB')
  GROUP BY
    1,
    2,
    3),
  Rankings AS (
  SELECT
    date,
    country_code,
    project_name,
    downloads,
    ROW_NUMBER() OVER (PARTITION BY date, country_code ORDER BY downloads DESC) AS rank
  FROM
    ProjectCounts)
SELECT
  date,
  country_code,
  project_name,
  downloads,
  rank
FROM
  Rankings
WHERE
  rank <= 50
